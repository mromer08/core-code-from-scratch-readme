# Simple calculator

## Description

For this challenge you will be performing a simple calculator using **Switch (Segun)**, this calculator can perform the following operations:

1. Sum (+)
2. Subtract (-)
3. Multiplication (\*)
4. Division (/)

The calculator must ask the user for two numbers, after asking for the two numbers, you must ask for the operation to be performed, keep in mind that you must show the user the options available (+, -, \*, /). The first thing that must be done is to validate that the operation that the user entered is valid, if it is not a valid option, the user must be shown an error message, for example: `⚠️ La operación no es valida` and terminate the program. If the operation is valid, show the message: `Procesando: <OPERACIÓN A REALIZAR>` For, example: if the user has entered the numbers 10 and 15 as well as the operation \*, the message should read: `Procesando: 10 * 15`. After this message the result of the operation must be displayed, following the previous example, the result of operating 10 \* 15 is 150, so the program should return: `Resultado: 150`. Remember to use **Switch (Segun)** to identify which operations you should execute.

## Helpful Resources

1. [PSeInt **Switch (Segun)**](https://www.youtube.com/watch?v=3UOlwCsDOjU)

# Solution
```python
Algoritmo SimpleCalculatorSwitch
	Definir num1,num2,result Como Real;
	Definir flag Como Logico
	Definir operator Como Caracter;
	flag <- Verdadero;
	Imprimir "[-----Simple Calculator-----]";
	Imprimir "Enter first number:";
	Leer num1;
	Imprimir "Enter second number:";
	Leer num2;
	Imprimir "Enter an operation (+,-,*,/)";
	Leer operator;
	Segun operator Hacer
		"+": result <- num1+num2;
		"-": result <- num1-num2;
		"*": result <- num1*num2;
		"/":
			Si num2 == 0 Entonces
				Imprimir "Error: Divide by zero";
				flag <- Falso;
			SiNo
				result <- num1/num2;
			FinSi
		De Otro Modo:
			Imprimir "Error: The operation doesnt exist";
			flag <- Falso;
	FinSegun
	Si flag Entonces
		Imprimir "Processing: ", ConvertirATexto(num1), " ",operator, " ", ConvertirATexto(num2);
		Imprimir  "Result: ", result;
	FinSi
FinAlgoritmo
```